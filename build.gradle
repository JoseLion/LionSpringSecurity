buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url 'http://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.spring.gradle:propdeps-plugin:0.0.10.RELEASE")
	}
}

apply plugin: 'java-library'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'

sourceCompatibility = 11
targetCompatibility = 11

group = 'com.github.joselion'
archivesBaseName = 'lion-spring-security'
version = '1.2.3'

jar {
	enabled = true
    manifest {
    	attributes("Export-Package": "com.github.joselion.lionspringsecurity.*", "-sources": "true")
    }
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives jar, javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			pom.project {
				name = "Lion Spring Security"
				packaging 'jar'
				description = "Library wapper to simply implement Spring Security to Spring Boot projects with additional features like locking and and expiration, by simpli adding the database account information to the application propertiies file"
				url = "https://github.com/JoseLion/LionSpringSecurity"
				
				licenses {
					license {
						name = "MIT License"
						url = "http://www.opensource.org/licenses/mit-license.php"
					}
				}
				
				developers {
					developer {
						id = "JoseLion"
						name = "Jose Luis Leon"
						email = "joseluis5000l@gmail.com"
						organization = "com.github.joselion"
						organizationUrl = "https://github.com/JoseLion"
					}
				}
				
				scm {
					connection = "scm:git:git@github.com/JoseLion/LionSpringSecurity.git"
					developerConnection = "scm:git:ssh://github.com:JoseLion/LionSpringSecurity.git"
					url = "https://github.com/JoseLion/LionSpringSecuritys"
				}
			}
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	provided('org.springframework.boot:spring-boot-starter-web')
	provided('org.springframework.boot:spring-boot-starter-security')
	provided('org.springframework.boot:spring-boot-starter-jdbc')
	optional('org.springframework.boot:spring-boot-configuration-processor')
	provided('javax.servlet:javax.servlet-api:4.0.1')
	implementation('commons-io:commons-io:2.6')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.security:spring-security-test')
	testImplementation('org.postgresql:postgresql')
}

eclipse {
	classpath {
		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-11'
	}
}
